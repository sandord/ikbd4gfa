' IKBD4GFA.INL
INLINE ikbd%,982
'
DIM reg%(15)
'
init_ikbd
'
ON ERROR GOSUB error_restore
scancode_tbl%=@get_scancode_state_table
ascii_tbl%=@get_ascii_state_table
'
PRINT "Please hold down keys/joystick a bit longer because the information here is"
PRINT "updated rather slowly."
PRINT "Press F9 to exit."
'
exit%=0
WHILE exit%=0
  VSYNC
  '
  scancode%=@get_last_scancode
  joy%=@get_joystick_states
  interrogate_joysticks
  '
  ' Print scancode state table.
  a$="SCANCODES: "
  FOR i%=0 TO 127
    a$=a$+CHR$(ASC("0")+PEEK(scancode_tbl%+i%))
  NEXT i%
  PRINT AT(1,8);a$
  '
  PRINT ascii;state;table.
  b$="ASCII: "
  FOR i%=0 TO 255
    b$=b$+CHR$(ASC("0")+PEEK(ascii_tbl%+i%))
  NEXT i%
  PRINT AT(1,12);b$
  '
  ' Print joystick state.
  PRINT AT(1,5);"JOY -> ";joy%;"  "
  '
  ' Print last scancode.
  PRINT AT(1,6);"SCN -> ";scancode%;"  "
  '
  IF PEEK(scancode_tbl%+67)=1 THEN !F9
    exit%=1
  ENDIF
WEND
'
restore_ikbd
'
PROCEDURE init_ikbd
  ~XBIOS(38,L:ikbd%+&H1C) !init
RETURN
PROCEDURE restore_ikbd
  ~XBIOS(38,L:ikbd%+&H20) !restore
RETURN
FUNCTION get_scancode_state_table
  RCALL ikbd%+&H24,reg%() !get_scancode_state_tbl
  RETURN reg%(8) !a0
ENDFUNC
FUNCTION get_ascii_state_table
  RCALL ikbd%+&H28,reg%() !get_ascii_state_tbl
  RETURN reg%(8) !a0
ENDFUNC
FUNCTION get_last_scancode
  RCALL ikbd%+&H2C,reg%()
  RETURN reg%(0) !d0
ENDFUNC
FUNCTION get_joystick_states
  RCALL ikbd%+&H30,reg%() !get_joystick_states
  RETURN reg%(0) !d0
ENDFUNC
PROCEDURE interrogate_joysticks
  ~XBIOS(38,L:ikbd%+&H34) !interrogate_joysticks
RETURN
PROCEDURE error_restore
  PRINT "ERROR ";ERR
  PRINT ERR$(ERR)
  restore_ikbd
  END
RETURN
